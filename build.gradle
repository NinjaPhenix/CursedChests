plugins {
    id "fabric-loom" version "0.2.6-SNAPSHOT"
    id "java"
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    archivesBaseName = project.archives_base_name
}

version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

repositories {
    maven { url "https://minecraft.curseforge.com/api/maven" }
    maven { url "https://tehnut.info/maven/" }
    flatDir { dirs "includes" }
}

dependencies {
    minecraft("com.mojang:minecraft:${minecraft_version}")
    mappings("net.fabricmc:yarn:${minecraft_version}+${yarn_version}:v2")
    modCompile("net.fabricmc:fabric-loader:${loader_version}")
    modCompile("net.fabricmc.fabric-api:fabric-api:${fabric_api_version}")

    modCompile("me.shedaniel:RoughlyEnoughItems:${rei_version}")
    {
        exclude(group: "net.fabricmc", module: "fabric-loader")
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "io.github.prospector", module: "modmenu")
    }

    modCompile("io.github.prospector:modmenu:${modmenu_version}")
    {
        exclude (group: "net.fabricmc", module: "fabric-loader")
        exclude (group: "net.fabricmc.fabric-api")
    }

    modCompile("towelette:Towelette:2.1.3+1.15") // Remove Only to add towlette to dev.
    modCompile("statement:Statement:1.2.0+1.14.4")    // Remove Only to add towlette to dev.

    modRuntime("ninjaphenix:creativebuttonmover:${cbm_version}")
    include("ninjaphenix:creativebuttonmover:${cbm_version}")

    modCompile("ninjaphenix:expandedstorageapi:${expandedstorageapi_version}")
    modCompile("ninjaphenix:ninjaphenix-container-lib:${containerlib_version}")
    include("ninjaphenix:expandedstorageapi:${expandedstorageapi_version}")
}

processResources {
    inputs.property "version", project.version
    from(sourceSets.main.resources.srcDirs) {
        include("fabric.mod.json")
        expand("version": version)
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude("fabric.mod.json")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

jar { from("LICENSE") }

runClient {
    args = ["--username", "Dev"]
}

tasks.withType(JavaCompile) { options.setEncoding("UTF-8") }

curseforge {
    apiKey = project.hasProperty("api_key") ? project.api_key : "" // Fix for other tasks failing due to missing property
    //noinspection GroovyAssignabilityCheck
    project {
        id = "317856"
        changelog = file("changelog.txt")
        releaseType = "${project.release_type}"
        addGameVersion "${project.game_version}"
        addGameVersion "Fabric"
        relations {
            requiredDependency "fabric-api"
        }
        mainArtifact remapJar
        addArtifact sourcesJar
    }
    options {
        forgeGradleIntegration = false
    }
}